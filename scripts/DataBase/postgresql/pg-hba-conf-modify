#!/usr/bin/perl
use FindBin;
use lib "$FindBin::Bin/../lib/perl-lib/lib/perl5";
use lib "$FindBin::Bin/../lib";

package HBAConfParser;

use strict;
use POSIX qw(uname strftime);
use IO::File;
use JSON;
use Getopt::Long;

use AutoExecUtils;

sub new {
    my ( $pkg, $filePath, $needBackup ) = @_;

    my $self = {
        filePath    => $filePath,
        needBackup  => $needBackup,
        confItems   => [],
        confItemMap => {}
    };

    bless( $self, $pkg );

    if ( defined($filePath) ) {
        if ( not -e $filePath ) {
            $self->{hasError} = 1;
            print("ERROR: $filePath not exists.\n");
        }
        if ( not -f $filePath ) {
            $self->{hasError} = 1;
            print("ERROR: $filePath is not a file.\n");
        }
    }

    if ( defined($filePath) ) {
        my ( $confItems, $confItemMap ) = $self->__read_config( $filePath, 'file' );
        $self->{confItems}   = $confItems;
        $self->{confItemMap} = $confItemMap;
    }

    return $self;
}

sub backup {
    my ( $self, $filePath ) = @_;
    my $dateTimeStr = strftime( "%Y%m%d-%H%M%S", localtime() );
    my $exitCode    = system(qq{cp "$filePath" "$filePath.$dateTimeStr"});
    return $exitCode;
}

sub getFileContent {
    my ( $self, $filePath ) = @_;
    my $content;

    if ( -f $filePath ) {
        my $size = -s $filePath;
        my $fh   = IO::File->new("<$filePath");

        if ( defined($fh) ) {
            $fh->read( $content, $size );
            $fh->close();
        }
        else {
            die("ERROR: File:$filePath open failed, $!.\n");
        }
    }

    return $content;
}

sub __read_config {
    my ( $self, $filePath ) = @_;
    my $content = $self->getFileContent($filePath);
    my ( $confItems, $confItemMap ) = $self->__parseConf($content);
    return ( $confItems, $confItemMap );
}

sub __parseConf {
    my ( $self, $content ) = @_;

    my @confItems   = ();
    my $confItemMap = {};

    my $comment = '';
    $content =~ s/^\s*|\s*$//g;
    foreach my $line ( split( /\n/, $content ) ) {
        $line =~ s/^\s*|\s*$//g;

        # handle comments
        if ( $line =~ /^[;#]/ ) {
            $comment = $comment . $line . "\n";
            next;
        }
        elsif ( $line eq '' ) {
            $comment = $comment . "\n";
            next;
        }

        my @fields = split( /\s+/, $line, 4 );
        my ( $type, $database, $address, $method );
        if ( scalar(@fields) == 4 ) {
            $type     = $fields[0];
            $database = $fields[1];
            $address  = $fields[2];
            $method   = $fields[3];
        }
        elsif ( scalar(@fields) == 3 ) {
            $type     = $fields[0];
            $database = $fields[1];
            $address  = '';
            $method   = $fields[2];
        }

        if ( defined($type) and defined($database) ) {
            my $item = {
                'TYPE'     => $type,
                'DATABASE' => $database,
                'ADDRESS'  => $address,
                'METHOD'   => $method,
                'COMMENT'  => $comment
            };
            my $key = "$type.$database.$address";
            push( @confItems, $item );
            $confItemMap->{$key} = $item;
        }
    }

    return ( \@confItems, $confItemMap );
}

sub modifyConf {
    my ( $self, $content ) = @_;

    my $confItems   = $self->{confItems};
    my $confItemMap = $self->{confItemMap};
    my ( $newItems, $newConfItemMap ) = $self->__parseConf($content);

    my $modified = 0;
    if ( defined($newItems) ) {
        foreach my $item (@$newItems) {
            my $key     = $item->{TYPE} . '.' . $item->{DATABASE} . '.' . $item->{ADDRESS};
            my $oldItem = $confItemMap->{$key};
            if ( defined($oldItem) ) {
                if ( $oldItem->{METHOD} ne $item->{METHOD} ) {
                    $modified = 1;
                    $oldItem->{METHOD} = $item->{METHOD};
                    print( "INFO: Change " . $item->{TYPE} . "\t" . $item->{DATABASE} . "\t" . $item->{ADDRESS} . "\t" . $item->{METHOD} . "\n" );
                }
                my $oldComment = $oldItem->{COMMENT};
                my $newComment = $item->{COMMENT};
                foreach my $commentLine ( split( /\n/, $newComment ) ) {
                    if ( index( $oldComment, $commentLine ) < 0 ) {
                        $modified = 1;
                        $oldItem->{COMMENT} = $oldComment . "\n" . $commentLine;
                    }
                }
            }
            else {
                $modified = 1;
                push( @$confItems, $item );
                $confItemMap->{$key} = $item;
                print( "INFO: Append " . $item->{TYPE} . "\t" . $item->{DATABASE} . "\t" . $item->{ADDRESS} . "\t" . $item->{METHOD} . "\n" );
            }
        }
    }

    my $newContent = '';
    foreach my $item (@$confItems) {
        $newContent = $newContent . $item->{COMMENT} . "\n";
        $newContent = $newContent . $item->{TYPE} . "\t" . $item->{DATABASE} . "\t" . $item->{ADDRESS} . "\t" . $item->{METHOD} . "\n";
    }

    if ( $modified == 1 ) {
        my $filePath = $self->{filePath};
        if ( $self->{needBackup} == 1 ) {
            $self->backup($filePath);
        }

        my $confFile = IO::File->new(">$filePath");
        if ( defined($confFile) ) {
            print $confFile ($newContent);
            $confFile->close();
        }
        else {
            print("ERROR: Can not write to file $filePath, $!\n");
            $self->{hasError} = 1;
        }
    }
    else {
        print("INFO: Config not changed.\n");
    }

    return $newContent;
}

sub usage {
    my $pname = $FindBin::Script;

    print("$pname --backup 0|1 --filepath <pg_hba.conf directory> --content <hosts content>\n");
    exit(1);
}

sub main {
    $| = 1;    #不对输出进行buffer，便于实时看到输出日志
    my $filePath;
    my $needBackup = 0;
    my $content;

    GetOptions(
        'backup=i'   => \$needBackup,
        'filepath=s' => \$filePath,
        'content=s'  => \$content
    );

    if ( not defined($filePath) or $filePath eq '' ) {
        print("ERROR: Must defined pg_hba.conf path by option --filepath\n");
        usage();
    }

    if ( not defined($content) or $content eq '' ) {
        print("ERROR: Must defined config content by option --content\n");
        usage();
    }

    my $confParser  = HBAConfParser->new( $filePath, $needBackup );
    my $confContent = $confParser->modifyConf($content);

    my $out = { hbaConf => $confContent };
    AutoExecUtils::saveOutput($out);

    return $confParser->{hasError};
}

exit main();

1;
